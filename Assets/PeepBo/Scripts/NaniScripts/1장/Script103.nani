@back ForestDay
@showUI EpisodeTitle
@wait 2
@hideUI EpisodeTitle
@char Y.Face>기본,Body>기본 position:-0.25,-15 scale:1
J: 으아~ 공기 좋다! 심심할 때마다 산책하기 좋겠어.
@sfx 슝
@char Y.Face>울먹,Body>기본 position:-0.25,-21.5 scale:1.3
Y: 으아, 나 진짜 죽을 거 같아...
@char Y.Face>놀란,Body>입에손 position:-0.25,-15 scale:1
Y: 누나 에너지 무슨 일이야... 이런 산에서, 산책이라니...
J: 엄살은~ 너 이래서 오기 싫어했구나?
@char Y.Face>아련,Body>기본
Y: 여기 오르내릴때마다 힘들어 죽겠다고...
J: 이 정도면 길도 고르고 경사도 별로 없네, 뭐.
@sfx 슝
@char Y.Face>웃는,Body>입에손 position:-0.25,-21.5 scale:1.3
Y: 누나도 곧... 곧 후회하게 될걸요?
@char Y.Face>웃는,Body>입에손 position:-0.25,-15 scale:1
@printer Narration time:0
응?
@hidePrinter
@hide Y
@back ForestDay visible:false time:1
... ...

@back ForestDay visible:true
@bgm 메인03 fade:0.5
@char Y.Face>기본,Body>기본
@spawn ShakeBackground wait:false
J: 헉헉... 도대체 여기가 어디야?
@printer Narration time:0
10분 정도 숲을 오르자 그나마 평평했던 길은 사라지고 사방에 돌과 나무뿌리 때문에 걷기 힘들어졌다.
@sfx 슝
@char Y.Face>웃는,Body>기본 position:-0.25,-21.5 scale:1.3
Y: 헉헉... 거봐! 여기 만만하지 않다니까!
@char Y.Face>웃는,Body>기본 position:-0.25,-15 scale:1
Y: 절대 내가 약한 게 아니라니까요!
@spawn ShakeBackground wait:false
J: 너...너 일부러 힘든 길로 골라가는 거지?!
@char Y.Face>놀란,Body>기본
Y: 아...닌데? 
@char Y.Face>아련,Body>기본
@printer Narration time:0
근데 왜 내 눈을 피하는 건데?
@char Y.Face>기본,Body>기본
나는 나무 기둥을 잡고 가쁜 숨을 몰아쉬었다.
주변을 돌아보아도 동서남북이 죄다 똑같은 풍경이었다.
J: 근데, 이 위로 쭉 올라가면 뭐가 있는데?
@printer Narration time:0
나는 수상쩍은 눈빛으로 유겸을 쳐다보며 물었다.
@char Y.Face>웃는,Body>기본
Y: 선락원이요! 섬의 유일한 교회예요.
J: 무슨 교회가 산 중턱에 있어?
@sfx 슝
@char Y.Face>윙크,Body>기본 position:-0.25,-21.5 scale:1.3
Y: 그곳이 해록도의 중심이니까요.
@char Y.Face>윙크,Body>기본 position:-0.25,-15 scale:1
@printer Narration time:0
장난이 잔뜩 베인 미소에 또다시 불신이 스멀스멀 올라오는데,

@sfx 발소리
@char Y.Face>기본,Body>기본
마침 맞은편에서 내려오는 사람들이 보였다.
@stopSfx 발소리
@char Headman visible:false avatar:Headman
@char Woman visible:false avatar:People
Headman: 다들 여기서 뭐 해?
@printer Narration time:0
첫날에 본 이장님이다! 유겸이 안내한 길이 틀리진 않는 모양이네.
@char Y.Face>웃는,Body>기본
J: 아, 안녕하세요.
Woman: 인사성도 밝지! 둘이 산책이라도 하는 거야?
@char Y.Face>윙크,Body>브이
Y: 제가 또 한 친절 하잖아요? 육지에서 온 귀한 손님에게 섬을 안내해주고 있었지요.
Headman: 잘했네! 우리 유겸 군이 해록도에 얼마나 빠삭한지 이보다 유능한 가이드는 없을 거야!
@char Y.Face>웃는,Body>기본
Y: 지금 누나 앞이라고 저 띄워주시는 거라면 사양말고 더 해주세요.
Headman: 그렇다면 지금 선락원에 가는 길이겠네! 그래, 제일 먼저 들러야지.
@char Y.Face>놀란,Body>기본
Woman: 그런데 편한 길 놔두고 왜 이리로 올라와? 여긴 우리가 운동할 겸 오르는 길인데...
@char Y.Face>아련,Body>입에손
@printer Narration time:0
....유겸이 또 내 눈을 피한다.
J: 하하하... 어쩐지 운동이 안 될 수가 없.겠.더.라.고.요.
@char Y.Face>웃는,Body>기본
Woman: 역시 젊음이 좋네~ 젊은이들이 있으니 해록도가 훨씬 밝아진 것 같아!
Headman: 밝아져? 밝아, 밝은...아차차!
@spawn ShakeBackground wait:false
Headman: 그러고 보니 마을회관에 전등이 나가버려서 유겸이를 부르려던 참이었는데!
@printer Narration time:0
@char Y.Face>기본,Body>기본
이장님이 깜박했다는 듯 외쳤다.
Headman: 나랑 같이 가서 전구 좀 갈아줘.
@char Y.Face>아련,Body>입에손
Y: 지금요? 오늘은 지안 누나를 위해 비워두기로 했는데...
Headman: 선락원이야 이리로 쭉 올라가면 금방 나오는데 뭘! 
@printer Narration time:0
@char Y.Face>기본,Body>기본
이장님의 하소연에 유겸은 제 머리를 긁적이며 내 눈치를 봤다.
어떡할까?
@startchoice 0
@choice "혼자 갈 수 있어." goto:.A
@choice "끝까지 데려다줘." goto:.B
@hidePrinter
@stop


#A
@printer Narration time:0
이장님 말을 들어보니 얼마 남지 않은 것 같은데...
@char Y.Face>웃는,Body>기본
하루종일 귀찮게 할 순 없지.
J: 그래, 이장님 말대로 이 정도는 혼자 갈 수 있을 것 같아.
@printer Narration time:0
@char Y.Face>아련,Body>입에손
내 반응에 영 아쉽다는 표정이다.
@char Y.Face>웃는,Body>기본
Y: 누나가 그렇다니 알겠어요! 그럼 나중에 또 봐요!
@char Y.Face>기본,Body>기본
Y: 아, 숲은 해가 금방 지니까 너무 오래 있지 말고.
Headman: 그래, 곧 주민들과 인사하는 자리를 마련하겠네~ 다음에 또 보세.
@printer Narration time:0
유겸이 주민들과 함께 멀어졌다.
@hide Y
@goto .Con1

#B
@printer Narration time:0
이장님 말을 들어보니 얼마 남지 않은 것 같지만... 
@char Y.Face>웃는,Body>기본
나도 당할 수만은 없으니까 일부러 서운한 티를 내며 유겸을 바라보았다.
@char Y.Face>기본,Body>기본
J: 뭐야, 오늘은 나를 위해 비워뒀다면서!
J: 그러면 끝까지 데려다줘야지, 나 길치란 말이야.
@printer Narration time:0
@char Y.Face>아련,Body>입에손
내 반응에 유겸이 웃음을 참는 표정이 되었다.
@char Y.Face>웃는,Body>입에손
Y: 알겠어요, 알겠어.
Y: 그 짧은 사이에 나 없으면 안 되는 사람이 되다니... 이 누나를 어떡하지?
@printer Narration time:0
아... 너무 뿌듯해하니까 살짝 후회되는데...
이장님도 매우 아쉬워하는 듯하니...
@char Y.Face>기본,Body>기본
나는 얄미운 미소를 머금은 채 유겸의 등짝을 찰싹 때렸다.
J: 장난은 이쯤 할게~ 어서 내려가 봐
J: 이장님 말대로 이 정도는 나 혼자 갈 수 있으니까.
@char Y.Face>놀란,Body>입에손
Y: 응...? 나 내려가라고?
J: 그래~ 이장님 다음에 또 뵈어요.
@printer Narration time:0
@char Y.Face>아련,Body>기본
유겸이 영 아쉽다는 얼굴로 돌아섰다.
@char Y.Face>웃는,Body>기본
Y: 누나가 그렇다니 알겠어요, 그럼 나중에 또 봐요!
Y: 아, 숲은 해가 금방 지니까 너무 오래 있지 말고.
Headman: 그래, 곧 주민들과 인사하는 자리를 마련하겠네~ 다음에 또 보세.
@printer Narration time:0
유겸이 주민들과 함께 멀어졌다.
@hide Y

#Con1
@hidePrinter
@stopBgm 메인03 fade:1
모두가 내려가자 주위가 순식간에 고요해졌다.
@bgm 숲_낮
J: 생태보호구역이라더니 정말 관리가 잘 되어 있네.
J: 핸드폰만 잘 터졌다면 더 좋았을 걸.
@printer Narration time:0
@sfx 핸드폰_에러
@char Phone.impossible position:0,-4 scale:0.7
여전히 먹통인 핸드폰의 화면을 두드리며 길이 난 숲 안쪽을 향해 걷기 시작했다.
@stopSfx 핸드폰_에러
@hide Phone
@sfx 발소리
J: 이장님이 이쪽으로 쭉 올라가면 선락원이랬지.
J: 잠깐, 이 방향이 맞나?
@stopSfx 발소리
@printer Narration time:0
몸을 틀어보았지만, 여전히 풍경은 비슷했다.
게다가 해가 금방 진다는 말은 거짓이 아니었던 듯, 좀 전보다 훨씬 어두워져있었다.
@hidePrinter
@stopBgm 숲_낮 fade:1
@back ForestDay visible:false time:1
@wait 1
@back ForestNight visible:true
@bgm 숲_밤
J: ...나 길치였던가.
@sfx 부스럭
@printer Narration time:0
어째 점점 더 깊숙한 곳으로 들어가는 것 같은데, 기분 탓일까.
J: 아우 피곤해, 배도 고프고... 산만 타다가 날 새겠네.
@printer Narration time:0
이만 내려가는 게 낫겠다는 생각이 들 때쯤, 나뭇가지 사이로 우뚝 솟은 십자가가 보였다.
@stopSfx 부스럭

@back ForestNight visible:false time:0
@back ChurchInTheForest pos:50,50
J: 저게... 선락원.
@printer Narration time:0
십자가는 마치 나를 부르는 손짓처럼 밝게 빛나고 있었다.
J: 왜 이렇게... 익숙하지?
@printer Narration time:0
어디선가 본 듯한...

@back ChurchInTheForest visible:false time:0
@back ForestNight
@spawn ShakeBackground wait:false
@sfx 떨어짐
J: 아앗!
@printer Narration time:0
멍하니 걸어 올라가다가 그만 튀어나온 나무 뿌리에 신발이 벗겨지고 말았다.
@stopSfx 떨어짐
J: 아야야... 하필 샌들을 신고 와서...
@printer Narration time:0
여기 유독 날카로운 돌멩이가 많네.
@sfx 부스럭
나는 저 멀리 굴러간 신발을 겨우 줍고 나서 다시 걷기 시작했다.
@stopSfx 부스럭
@hidePrinter

@back ForestNight visible:false time:0
@back ParadiseOutsideNight time:1
@wait 2
@printer Narration time:0
... ...
긴장을 품은 채 가까이 다가가 보니 의외로 평범한 교회 건물이었다.
@hidePrinter
@sfx 장소발견 wait:false
@showUI ParaFindUI
@wait i
@hideUI ParaFindUI

@printer Narration time:0
불이 다 꺼져있는 걸로 보아 아무도 없는 모양이네.
J: 들어가도... 될까?
@printer Narration time:0
잠시 서서 공기를 들이마신 뒤, 나는....
@startchoice 1
@choice "노크한다." goto:.C
@choice "문고리를 당긴다." goto:.D
@hidePrinter
@stop

#C
@sfx 노크
@printer Narration time:0
조심스럽게 문을 두드렸다.
J: ... ...
@sfx 문여는_나무문
@printer Narration time:0
아무 소리도 들리지 않는 듯한데, 일단 들어가 볼까.
@stopSfx 문여는_나무문
@goto .Con2
#D
@sfx 문여는_나무문
@printer Narration time:0
조심스럽게 문고리를 당겼다.
@stopSfx 문여는_나무문

#Con2
@hidePrinter
@stopBgm 숲_밤
@bgm 다함_등장
@back ParadiseOutsideNight visible:false time:1
@back ParadiseInsideNight time:1
@printer Narration time:0
아무도 없다고 생각했는데…
@char Silhouette.D scale:1.23 position:0,-15.6
스테인드 글라스의 빛을 받으며 한 남자가 서 있었다.
남자는 나를 돌아보지 않은 채 말했다.
First: 달이 타오를 듯 밝은 밤이군요.
@spawn ShakeBackground wait:false
J: …!
First: 이 야심한 시각에,
First: 당신은 무엇에 이끌려 여기까지 오셨습니까.
@hide Silhouette wait:false
@sfx 챠임벨
@char D.Face>기본,Body>가슴에손 position:0,-16
First: 새벽 예배를 온 건 아닌 것 같은데.
@char D.Face>웃는,Body>가슴에손
First: 제가 어떤 인사를 드려야 할 지...
@stopSfx 챠임벨
@printer Narration time:0
남자는 다가오며 희미하게 웃었고 그 순간 줄곧 멍하던 내 정신도 깨어났다.
J: 아... 안녕하세요?
@char D.Face>웃는,Body>가슴에손
First: 신께서 당신을 이곳으로 인도했나 보군요.
J: ...네? 아... 신께서...
@printer Narration time:0
아참... 여기 교회였지.
J: 여기가 그...

@hideAll
@back ForestDay
@camera set:RememberEffect.true wait:false
@char Y.Face>기본,Body>기본 position:-0.25,-15 scale:1
J: 근데, 이 위로 쭉 올라가면 뭐가 있는데?
Y: 선락원이요! 섬의 유일한 교회예요.
J: 무슨 교회가 산 중턱에 있어?
@sfx 슝
@char Y.Face>윙크,Body>기본
Y: 그곳이 해록도의 중심이니까요.
@hideAll
@camera set:RememberEffect.false
@back ParadiseInsideNight
@char D.Face>기본,Body>기본 position:0,-16

@printer Narration time:0
해록도의 유일한 교회... 선락원이라고 했지.
J: 당신은...
D: 주다함, 이라고 해요
@startchoice 2
@choice "이름이 특이하네요?" goto:.E
@choice "이름이 참 예쁘네요." goto:.F target:D score:5
@hidePrinter
@stop

#E
J: 주다함…? 이름이 특이하네요?
@char D.Face>웃는,Body>입에손
D: 그런가요? 전 익숙해서, 잘 모르겠네요.
@goto .Con3
#F
; 주다함 호감도 +5
J: 주다함…? 이름이 참 예쁘네요.
@char D.Face>웃는,Body>입에손
D: 고마워요. 이름이 예쁘다는 말은... 오랜만에 듣네요.

#Con3
J: 아, 저는 며칠 전에 해록도에 들어온 김지안이에요.
@char D.Face>아련,Body>입에손
D: …김지안.
@printer Narration time:0
그가 성경 구절을 읽듯 나지막이 내 이름을 중얼거렸다.
J: 그런데 왜 여기 계시는 거예요?
@printer Narration time:0
예배 시간은 아닌 것 같은데...
D: 저는 선락원에 살고 있어요. 지금은, 기도 중이었습니다.
@printer Narration time:0
목사님이라기엔 심하게 젊어보이는데 왜 선락원에 살고 있는 거지?
물어보고 싶었지만 개인적인 사정이 있을 수도 있고 왠지 분위기가 어색하니까...
@char D.Face>기본,Body>기본
J: 아무도 없을 줄 알고 들어온 건데, 제가 방해를 했네요.
J: ...그럼 저는 이만 돌아가 볼게요.
@hide D
@sfx 문여는_소리
@printer Narration time:0
황급히 돌아서서 문고리를 힘껏 잡아당겼는데,
@stopSfx 문여는_소리
...응?
@sfx 문닫는_소리
다시 닫혔다?
@stopSfx 문닫는_소리
의아해서 고개를 돌리니 어느새 기척도 없이 다가온 다함의 곧게 뻗은 팔이 보였다
@char D.Face>기본,Body>기본 position:0,-16
J: …?
J: 문을 왜 닫으시는…
@char D.Face>걱정,Body>입에손
D: 상처가...
J: 네?
@sfx 슝
@char D.Face>아련,Body>입에손 position:0,-23 scale:1.3
D: 발을 다치신 건가요?
@printer Narration time:0
@char D.Face>아련,Body>입에손 position:0,-16 scale:1
그의 걱정어린 시선을 아래로 내렸다
J: 아아....?
@printer Narration time:0
그의 걱정어린 시선을 아래로 내렸다.
아까 신발이 벗겨졌을 때 날카로운 조각에 긁혔던 건지 드러난 발에 피가 맺혀있었다.
J: 아, 이건...
@printer Narration time:0
@char D.Face>아련,Body>가슴에손
다함은 한쪽 무릎을 꿇고 앉더니 말릴 새도 없이 내 발목에 손을 가져다 댔다.
; 진동
J: 앗! 저, 저기요!
@char D.Face>기본,Body>가슴에손
D: 걸음이 불편해 보인 이유가 이거였군요.
D: 얼핏 봐도 상당히 따가웠을 텐데...
@printer Narration time:0
다함이 미간을 살포시 찌푸리며 말했다.
@char D.Face>아련,Body>기본
이 사람...정말 눈썰미가 좋네? 내 몸인데도 정신없어서 알아채지 못한 부분인데...
J: 괜, 괜찮아요. 천천히 내려가면 돼요.
@sfx 슝
@char D.Face>걱정,Body>가슴에손 position:0,-23 scale:1.3
D: 이대로 걸으면 상처가 덧날 수도 있어요, 길이 험하진 않아도 여긴 중턱이라.
@char D.Face>웃는,Body>가슴에손 position:0,-16 scale:1
D: 안쪽에 간단하게 치료할 수 있는 약이 있으니 잠시 기다리세요.
@printer Narration time:0
그는 나를 올려다보며 부드럽게 웃었다.
@char D.Face>기본,Body>기본
어쩐지 거부할 수 없는 목소리였다.
J: ...감사합니다.
@printer Narration time:0
@sfx 부스럭
@char D.Face>아련,Body>가슴에손
결국 나는 그가 이끄는 대로 의자에 앉았다.
@char D.Face>걱정,Body>가슴에손
작은 구급상자를 들고 온 다함은, 역시나 한쪽 무릎을 꿇고 앉아 내 발을 살폈다.
아니 그래도 오늘 처음보는 사이에 좀 미안하고 민망한데…
@char D.Face>기본,Body>기본
그냥 내가 하겠다고 할까?
@stopSfx 부스럭
@startchoice 3
@choice "내가 치료하겠다고 한다" goto:.G
@choice "가만히 그가 하는 대로 내버려 둔다" goto:.H target:D score:10
@hidePrinter
@stop

#G
J: 제가 할게요, 저 주세요.
@printer Narration time:0
@char D.Face>웃는,Body>기본
그는 연고를 향해 황급히 뻗은 내 손을 잡아 내리며 대답 대신 온화한 미소로 내 말을 가볍게 무시했다.
@goto .Continue4
#H
@printer Narration time:0
@char D.Face>웃는,Body>기본
아니야, 상처가 잘 보이지도 않는데 내가 뭘 어쩌겠어. 초면에 미안하지만 신세 좀 지자.

#Continue4
@hide D
@hidePrinter
@back ParadiseInsideNight visible:false
@movie DMovie time:0.5
@back DIllust time:0.3
@spawn DEffect
@wait i
@spawn ShakeCamera wait:false
J: 아앗!
@printer Narration time:0
그가 내 발에 손을 대자 나도 모르게 입에서 신음이 흘러나왔다.
J: 으으, 따가워...
D: 약이 스며들고 나면 괜찮아질 겁니다.
@printer Narration time:0
그는 부지런히 내 상처를 치료했다.
1분이 1시간처럼 느껴지는 고요하고 무거운 침묵을 고스란히 견디던 그때,
다함이 치료하다 말고 갑작스럽게 고개를 들어 눈을 맞춰왔다.

@despawn DEffect wait:false
@back DIllust visible:false time:0
@back ParadiseInsideNight
@char D.Face>기본,Body>기본 position:0,-16
D: 기다리고 있었습니다.
@printer Narration time:0
새하얀 피부와 스테인드글라스처럼 영롱하게 빛나는 눈동자.
나는 대답마저 잊은 채 그를 빤히 바라보다 더듬거리며 대답했다.
J: 네? 아... 기다려요? 누구를… 저를요?
@char D.Face>아련,Body>가슴에손
D: 네, 당신을요.
@sfx 슝
@char D.Face>웃는,Body>가슴에손 position:0,-23 scale:1.3
D: 아마도... 신께서?
@printer Narration time:0
@char D.Face>기본,Body>기본 position:0,-16 scale:1
무슨 말을 했냐는 듯 다함은 다시 시선을 내리고 반창고로 내 발을 감쌌다.
그의 목에 걸려있는 십자가 때문인지 나긋한 말투 때문인지,
@char D.Face>아련,Body>기본
치료보다는 하나의 경건한 의식처럼 보였다.
처음 보는 사람 앞에 맨발을 드러내서 그런 걸까 온몸이 뜨겁게 달아오르는 기분이 들었다.
어찌 됐든… 나쁜 사람은 아닌 것 같아.
@char D.Face>기본,Body>기본
그러자 혼자서 팽팽히 붙잡고 있던 긴장이 풀렸다.

@hidePrinter
@hide D
@back ParadiseInsideNight visible:false time:2
@endscript